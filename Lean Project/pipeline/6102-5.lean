import Mathlib.Analysis.Calculus.FDeriv.Linear
import Mathlib.Analysis.Calculus.FDeriv.Comp
import Mathlib.Analysis.Calculus.FDeriv.Linear
import Mathlib.Analysis.Calculus.FDeriv.Comp

/-!
# Additive operations on derivatives

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of

* sum of finitely many functions
* multiplication of a function by a scalar constant
* negative of a function
* subtraction of two functions
-/


open Filter Asymptotics ContinuousLinearMap

noncomputable section

section

variable {ð•œ : Type*} [NontriviallyNormedField ð•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ð•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ð•œ F]
variable {f g : E â†’ F}
variable {f' g' : E â†’L[ð•œ] F}
variable {x : E}
variable {s : Set E}
variable {L : Filter E}

section ConstSMul

variable {R : Type*} [Semiring R] [Module R F] [SMulCommClass ð•œ R F] [ContinuousConstSMul R F]

/-! ### Derivative of a function multiplied by a constant -/

@[fun_prop]
example (h : HasStrictFDerivAt f f' x) (c : R) :
    HasStrictFDerivAt (fun x => c â€¢ f x) (c â€¢ f') x :=
  (c â€¢ (1 : F â†’L[ð•œ] F)).hasStrictFDerivAt.comp x h

theorem HasFDerivAtFilter.const_smul (h : HasFDerivAtFilter f f' x L) (c : R) :
    HasFDerivAtFilter (fun x => c â€¢ f x) (c â€¢ f') x L :=
  (c â€¢ (1 : F â†’L[ð•œ] F)).hasFDerivAtFilter.comp x h tendsto_map

@[fun_prop]
nonrec theorem HasFDerivWithinAt.const_smul (h : HasFDerivWithinAt f f' s x) (c : R) :
    HasFDerivWithinAt (fun x => c â€¢ f x) (c â€¢ f') s x :=
  h.const_smul c

@[fun_prop]
nonrec theorem HasFDerivAt.const_smul (h : HasFDerivAt f f' x) (c : R) :
    HasFDerivAt (fun x => c â€¢ f x) (c â€¢ f') x :=
  h.const_smul c

@[fun_prop]
theorem DifferentiableWithinAt.const_smul (h : DifferentiableWithinAt ð•œ f s x) (c : R) :
    DifferentiableWithinAt ð•œ (fun y => c â€¢ f y) s x :=
  (h.hasFDerivWithinAt.const_smul c).differentiableWithinAt

@[fun_prop]
theorem DifferentiableAt.const_smul (h : DifferentiableAt ð•œ f x) (c : R) :
    DifferentiableAt ð•œ (fun y => c â€¢ f y) x :=
  (h.hasFDerivAt.const_smul c).differentiableAt

@[fun_prop]
theorem DifferentiableOn.const_smul (h : DifferentiableOn ð•œ f s) (c : R) :
    DifferentiableOn ð•œ (fun y => c â€¢ f y) s := fun x hx => (h x hx).const_smul c

@[fun_prop]
theorem Differentiable.const_smul (h : Differentiable ð•œ f) (c : R) :
    Differentiable ð•œ fun y => c â€¢ f y := fun x => (h x).const_smul c

theorem fderivWithin_const_smul (hxs : UniqueDiffWithinAt ð•œ s x)
    (h : DifferentiableWithinAt ð•œ f s x) (c : R) :
    fderivWithin ð•œ (fun y => c â€¢ f y) s x = c â€¢ fderivWithin ð•œ f s x :=
  (h.hasFDerivWithinAt.const_smul c).fderivWithin hxs

/-- Version of `fderivWithin_const_smul` written with `c â€¢ f` instead of `fun y â†¦ c â€¢ f y`. -/
theorem fderivWithin_const_smul' (hxs : UniqueDiffWithinAt ð•œ s x)
    (h : DifferentiableWithinAt ð•œ f s x) (c : R) :
    fderivWithin ð•œ (c â€¢ f) s x = c â€¢ fderivWithin ð•œ f s x :=
  fderivWithin_const_smul hxs h c

theorem fderiv_const_smul (h : DifferentiableAt ð•œ f x) (c : R) :
    fderiv ð•œ (fun y => c â€¢ f y) x = c â€¢ fderiv ð•œ f x :=
  (h.hasFDerivAt.const_smul c).fderiv

/-- Version of `fderiv_const_smul` written with `c â€¢ f` instead of `fun y â†¦ c â€¢ f y`. -/
theorem fderiv_const_smul' (h : DifferentiableAt ð•œ f x) (c : R) :
    fderiv ð•œ (c â€¢ f) x = c â€¢ fderiv ð•œ f x :=
  (h.hasFDerivAt.const_smul c).fderiv

end ConstSMul

section Add

/-! ### Derivative of the sum of two functions -/


@[fun_prop]
nonrec theorem HasStrictFDerivAt.add (hf : HasStrictFDerivAt f f' x)
    (hg : HasStrictFDerivAt g g' x) : HasStrictFDerivAt (fun y => f y + g y) (f' + g') x :=
   .of_isLittleO <| (hf.isLittleO.add hg.isLittleO).congr_left fun y => by
    simp only [LinearMap.sub_apply, LinearMap.add_apply, map_sub, map_add, add_apply]
    abel

theorem HasFDerivAtFilter.add (hf : HasFDerivAtFilter f f' x L)
    (hg : HasFDerivAtFilter g g' x L) : HasFDerivAtFilter (fun y => f y + g y) (f' + g') x L :=
  .of_isLittleO <| (hf.isLittleO.add hg.isLittleO).congr_left fun _ => by
    simp only [LinearMap.sub_apply, LinearMap.add_apply, map_sub, map_add, add_apply]
    abel

@[fun_prop]
nonrec theorem HasFDerivWithinAt.add (hf : HasFDerivWithinAt f f' s x)
    (hg : HasFDerivWithinAt g g' s x) : HasFDerivWithinAt (fun y => f y + g y) (f' + g') s x :=
  hf.add hg

@[fun_prop]
nonrec theorem HasFDerivAt.add (hf : HasFDerivAt f f' x) (hg : HasFDerivAt g g' x) :
    HasFDerivAt (fun x => f x + g x) (f' + g') x :=
  hf.add hg

@[fun_prop]
theorem DifferentiableWithinAt.add (hf : DifferentiableWithinAt ð•œ f s x)
    (hg : DifferentiableWithinAt ð•œ g s x) : DifferentiableWithinAt ð•œ (fun y => f y + g y) s x :=
  (hf.hasFDerivWithinAt.add hg.hasFDerivWithinAt).differentiableWithinAt

@[simp, fun_prop]
theorem DifferentiableAt.add (hf : DifferentiableAt ð•œ f x) (hg : DifferentiableAt ð•œ g x) :
    DifferentiableAt ð•œ (fun y => f y + g y) x :=
  (hf.hasFDerivAt.add hg.hasFDerivAt).differentiableAt

@[fun_prop]
theorem DifferentiableOn.add (hf : DifferentiableOn ð•œ f s) (hg : DifferentiableOn ð•œ g s) :
    DifferentiableOn ð•œ (fun y => f y + g y) s := fun x hx => (hf x hx).add (hg x hx)

@[simp, fun_prop]
theorem Differentiable.add (hf : Differentiable ð•œ f) (hg : Differentiable ð•œ g) :
    Differentiable ð•œ fun y => f y + g y := fun x => (hf x).add (hg x)

theorem fderivWithin_add (hxs : UniqueDiffWithinAt ð•œ s x) (hf : DifferentiableWithinAt ð•œ f s x)
    (hg : DifferentiableWithinAt ð•œ g s x) :
    fderivWithin ð•œ (fun y => f y + g y) s x = fderivWithin ð•œ f s x + fderivWithin ð•œ g s x :=
  (hf.hasFDerivWithinAt.add hg.hasFDerivWithinAt).fderivWithin hxs

/-- Version of `fderivWithin_add` where the function is written as `f + g` instead
of `fun y â†¦ f y + g y`. -/
theorem fderivWithin_add' (hxs : UniqueDiffWithinAt ð•œ s x) (hf : DifferentiableWithinAt ð•œ f s x)
    (hg : DifferentiableWithinAt ð•œ g s x) :
    fderivWithin ð•œ (f + g) s x = fderivWithin ð•œ f s x + fderivWithin ð•œ g s x :=
  fderivWithin_add hxs hf hg

theorem fderiv_add (hf : DifferentiableAt ð•œ f x) (hg : DifferentiableAt ð•œ g x) :
    fderiv ð•œ (fun y => f y + g y) x = fderiv ð•œ f x + fderiv ð•œ g x :=
  (hf.hasFDerivAt.add hg.hasFDerivAt).fderiv

/-- Version of `fderiv_add` where the function is written as `f + g` instead
of `fun y â†¦ f y + g y`. -/
theorem fderiv_add' (hf : DifferentiableAt ð•œ f x) (hg : DifferentiableAt ð•œ g x) :
    fderiv ð•œ (f + g) x = fderiv ð•œ f x + fderiv ð•œ g x :=
  fderiv_add hf hg

@[simp]
theorem hasFDerivAtFilter_add_const_iff (c : F) :
    HasFDerivAtFilter (f Â· + c) f' x L â†” HasFDerivAtFilter f f' x L := by
  simp [hasFDerivAtFilter_iff_isLittleOTVS]

alias âŸ¨_, HasFDerivAtFilter.add_constâŸ© := hasFDerivAtFilter_add_const_iff

@[simp]
theorem hasStrictFDerivAt_add_const_iff (c : F) :
    HasStrictFDerivAt (f Â· + c) f' x â†” HasStrictFDerivAt f f' x := by
  simp [hasStrictFDerivAt_iff_isLittleO]

@[fun_prop]
alias âŸ¨_, HasStrictFDerivAt.add_constâŸ© := hasStrictFDerivAt_add_const_iff

@[simp]
theorem hasFDerivWithinAt_add_const_iff (c : F) :
    HasFDerivWithinAt (f Â· + c) f' s x â†” HasFDerivWithinAt f f' s x :=
  hasFDerivAtFilter_add_const_iff c

@[fun_prop]
alias âŸ¨_, HasFDerivWithinAt.add_constâŸ© := hasFDerivWithinAt_add_const_iff

@[simp]
theorem hasFDerivAt_add_const_iff (c : F) : HasFDerivAt (f Â· + c) f' x â†” HasFDerivAt f f' x :=
  hasFDerivAtFilter_add_const_iff c

@[fun_prop]
alias âŸ¨_, HasFDerivAt.add_constâŸ© := hasFDerivAt_add_const_iff

@[simp]
theorem differentiableWithinAt_add_const_iff (c : F) :
    DifferentiableWithinAt ð•œ (fun y => f y + c) s x â†” DifferentiableWithinAt ð•œ f s x :=
  exists_congr fun _ â†¦ hasFDerivWithinAt_add_const_iff c

@[fun_prop]
alias âŸ¨_, DifferentiableWithinAt.add_constâŸ© := differentiableWithinAt_add_const_iff

@[simp]
theorem differentiableAt_add_const_iff (c : F) :
    DifferentiableAt ð•œ (fun y => f y + c) x â†” DifferentiableAt ð•œ f x :=
  exists_congr fun _ â†¦ hasFDerivAt_add_const_iff c

@[fun_prop]
alias âŸ¨_, DifferentiableAt.add_constâŸ© := differentiableAt_add_const_iff

@[simp]
theorem differentiableOn_add_const_iff (c : F) :
    DifferentiableOn ð•œ (fun y => f y + c) s â†” DifferentiableOn ð•œ f s :=
  forallâ‚‚_congr fun _ _ â†¦ differentiableWithinAt_add_const_iff c

@[fun_prop]
alias âŸ¨_, DifferentiableOn.add_constâŸ© := differentiableOn_add_const_iff

@[simp]
theorem differentiable_add_const_iff (c : F) :
    (Differentiable ð•œ fun y => f y + c) â†” Differentiable ð•œ f :=
  forall_congr' fun _ â†¦ differentiableAt_add_const_iff c

@[fun_prop]
alias âŸ¨_,  Differentiable.add_constâŸ© := differentiable_add_const_iff

@[simp]
theorem fderivWithin_add_const (c : F) :
    fderivWithin ð•œ (fun y => f y + c) s x = fderivWithin ð•œ f s x := by
  classical simp [fderivWithin]

@[simp]
theorem fderiv_add_const (c : F) : fderiv ð•œ (fun y => f y + c) x = fderiv ð•œ f x := by
  simp only [â† fderivWithin_univ, fderivWithin_add_const]

@[simp]
theorem hasFDerivAtFilter_const_add_iff (c : F) :
    HasFDerivAtFilter (c + f Â·) f' x L â†” HasFDerivAtFilter f f' x L := by
  simpa only [add_comm] using hasFDerivAtFilter_add_const_iff c

alias âŸ¨_, HasFDerivAtFilter.const_addâŸ© := hasFDerivAtFilter_const_add_iff

@[simp]
theorem hasStrictFDerivAt_const_add_iff (c : F) :
    HasStrictFDerivAt (c + f Â·) f' x â†” HasStrictFDerivAt f f' x := by
  simpa only [add_comm] using hasStrictFDerivAt_add_const_iff c

@[fun_prop]
alias âŸ¨_, HasStrictFDerivAt.const_addâŸ© := hasStrictFDerivAt_const_add_iff

@[simp]
theorem hasFDerivWithinAt_const_add_iff (c : F) :
    HasFDerivWithinAt (c + f Â·) f' s x â†” HasFDerivWithinAt f f' s x :=
  hasFDerivAtFilter_const_add_iff c

@[fun_prop]
alias âŸ¨_, HasFDerivWithinAt.const_addâŸ© := hasFDerivWithinAt_const_add_iff

@[simp]
theorem hasFDerivAt_const_add_iff (c : F) : HasFDerivAt (c + f Â·) f' x â†” HasFDerivAt f f' x :=
  hasFDerivAtFilter_const_add_iff c

@[fun_prop]
alias âŸ¨_, HasFDerivAt.const_addâŸ© := hasFDerivAt_const_add_iff

@[simp]
theorem differentiableWithinAt_const_add_iff (c : F) :
    DifferentiableWithinAt ð•œ (fun y => c + f y) s x â†” DifferentiableWithinAt ð•œ f s x :=
  exists_congr fun _ â†¦ hasFDerivWithinAt_const_add_iff c

@[fun_prop]
alias âŸ¨_, DifferentiableWithinAt.const_addâŸ© := differentiableWithinAt_const_add_iff

@[simp]
theorem differentiableAt_const_add_iff (c : F) :
    DifferentiableAt ð•œ (fun y => c + f y) x â†” DifferentiableAt ð•œ f x :=
  exists_congr fun _ â†¦ hasFDerivAt_const_add_iff c

@[fun_prop]
alias âŸ¨_, DifferentiableAt.const_addâŸ© := differentiableAt_const_add_iff

@[simp]
theorem differentiableOn_const_add_iff (c : F) :
    DifferentiableOn ð•œ (fun y => c + f y) s â†” DifferentiableOn ð•œ f s :=
  forallâ‚‚_congr fun _ _ â†¦ differentiableWithinAt_const_add_iff c

@[fun_prop]
alias âŸ¨_, DifferentiableOn.const_addâŸ© := differentiableOn_const_add_iff

@[simp]
theorem differentiable_const_add_iff (c : F) :
    (Differentiable ð•œ fun y => c + f y) â†” Differentiable ð•œ f :=
  forall_congr' fun _ â†¦ differentiableAt_const_add_iff c

@[fun_prop]
alias âŸ¨_, Differentiable.const_addâŸ© := differentiable_const_add_iff

@[simp]
theorem fderivWithin_const_add (c : F) :
    fderivWithin ð•œ (fun y => c + f y) s x = fderivWithin ð•œ f s x := by
  simpa only [add_comm] using fderivWithin_add_const c

@[simp]
theorem fderiv_const_add (c : F) : fderiv ð•œ (fun y => c + f y) x = fderiv ð•œ f x := by
  simp only [add_comm c, fderiv_add_const]

end Add

section Sum

/-! ### Derivative of a finite sum of functions -/


variable {Î¹ : Type*} {u : Finset Î¹} {A : Î¹ â†’ E â†’ F} {A' : Î¹ â†’ E â†’L[ð•œ] F}

@[fun_prop]
theorem HasStrictFDerivAt.sum (h : âˆ€ i âˆˆ u, HasStrictFDerivAt (A i) (A' i) x) :
    HasStrictFDerivAt (fun y => âˆ‘ i âˆˆ u, A i y) (âˆ‘ i âˆˆ u, A' i) x := by
  simp only [hasStrictFDerivAt_iff_isLittleO] at *
  convert IsLittleO.sum h
  simp [Finset.sum_sub_distrib, ContinuousLinearMap.sum_apply]

theorem HasFDerivAtFilter.sum (h : âˆ€ i âˆˆ u, HasFDerivAtFilter (A i) (A' i) x L) :
    HasFDerivAtFilter (fun y => âˆ‘ i âˆˆ u, A i y) (âˆ‘ i âˆˆ u, A' i) x L := by
  simp only [hasFDerivAtFilter_iff_isLittleO] at *
  convert IsLittleO.sum h
  simp [ContinuousLinearMap.sum_apply]

@[fun_prop]
theorem HasFDerivWithinAt.sum (h : âˆ€ i âˆˆ u, HasFDerivWithinAt (A i) (A' i) s x) :
    HasFDerivWithinAt (fun y => âˆ‘ i âˆˆ u, A i y) (âˆ‘ i âˆˆ u, A' i) s x :=
  HasFDerivAtFilter.sum h

@[fun_prop]
theorem HasFDerivAt.sum (h : âˆ€ i âˆˆ u, HasFDerivAt (A i) (A' i) x) :
    HasFDerivAt (fun y => âˆ‘ i âˆˆ u, A i y) (âˆ‘ i âˆˆ u, A' i) x :=
  HasFDerivAtFilter.sum h

@[fun_prop]
theorem DifferentiableWithinAt.sum (h : âˆ€ i âˆˆ u, DifferentiableWithinAt ð•œ (A i) s x) :
    DifferentiableWithinAt ð•œ (fun y => âˆ‘ i âˆˆ u, A i y) s x :=
  HasFDerivWithinAt.differentiableWithinAt <|
    HasFDerivWithinAt.sum fun i hi => (h i hi).hasFDerivWithinAt

@[simp, fun_prop]
theorem DifferentiableAt.sum (h : âˆ€ i âˆˆ u, DifferentiableAt ð•œ (A i) x) :
    DifferentiableAt ð•œ (fun y => âˆ‘ i âˆˆ u, A i y) x :=
  HasFDerivAt.differentiableAt <| HasFDerivAt.sum fun i hi => (h i hi).hasFDerivAt

@[fun_prop]
theorem DifferentiableOn.sum (h : âˆ€ i âˆˆ u, DifferentiableOn ð•œ (A i) s) :
    DifferentiableOn ð•œ (fun y => âˆ‘ i âˆˆ u, A i y) s := fun x hx =>
  DifferentiableWithinAt.sum fun i hi => h i hi x hx

@[simp, fun_prop]
theorem Differentiable.sum (h : âˆ€ i âˆˆ u, Differentiable ð•œ (A i)) :
    Differentiable ð•œ fun y => âˆ‘ i âˆˆ u, A i y := fun x => DifferentiableAt.sum fun i hi => h i hi x

theorem fderivWithin_sum (hxs : UniqueDiffWithinAt ð•œ s x)
    (h : âˆ€ i âˆˆ u, DifferentiableWithinAt ð•œ (A i) s x) :
    fderivWithin ð•œ (fun y => âˆ‘ i âˆˆ u, A i y) s x = âˆ‘ i âˆˆ u, fderivWithin ð•œ (A i) s x :=
  (HasFDerivWithinAt.sum fun i hi => (h i hi).hasFDerivWithinAt).fderivWithin hxs

theorem fderiv_sum (h : âˆ€ i âˆˆ u, DifferentiableAt ð•œ (A i) x) :
    fderiv ð•œ (fun y => âˆ‘ i âˆˆ u, A i y) x = âˆ‘ i âˆˆ u, fderiv ð•œ (A i) x :=
  (HasFDerivAt.sum fun i hi => (h i hi).hasFDerivAt).fderiv

end Sum

section Neg

/-! ### Derivative of the negative of a function -/


@[fun_prop]
theorem HasStrictFDerivAt.neg (h : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (fun x => -f x) (-f') x :=
  (-1 : F â†’L[ð•œ] F).hasStrictFDerivAt.comp x h

theorem HasFDerivAtFilter.neg (h : HasFDerivAtFilter f f' x L) :
    HasFDerivAtFilter (fun x => -f x) (-f') x L :=
  (-1 : F â†’L[ð•œ] F).hasFDerivAtFilter.comp x h tendsto_map

@[fun_prop]
nonrec theorem HasFDerivWithinAt.neg (h : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (fun x => -f x) (-f') s x :=
  h.neg

@[fun_prop]
nonrec theorem HasFDerivAt.neg (h : HasFDerivAt f f' x) : HasFDerivAt (fun x => -f x) (-f') x :=
  h.neg

@[fun_prop]
theorem DifferentiableWithinAt.neg (h : DifferentiableWithinAt ð•œ f s x) :
    DifferentiableWithinAt ð•œ (fun y => -f y) s x :=
  h.hasFDerivWithinAt.neg.differentiableWithinAt

@[simp]
theorem differentiableWithinAt_neg_iff :
    DifferentiableWithinAt ð•œ (fun y => -f y) s x â†” DifferentiableWithinAt ð•œ f s x :=
  âŸ¨fun h => by simpa only [neg_neg] using h.neg, fun h => h.negâŸ©

@[fun_prop]
theorem DifferentiableAt.neg (h : DifferentiableAt ð•œ f x) : DifferentiableAt ð•œ (fun y => -f y) x :=
  h.hasFDerivAt.neg.differentiableAt

@[simp]
theorem differentiableAt_neg_iff : DifferentiableAt ð•œ (fun y => -f y) x â†” DifferentiableAt ð•œ f x :=
  âŸ¨fun h => by simpa only [neg_neg] using h.neg, fun h => h.negâŸ©

@[fun_prop]
theorem DifferentiableOn.neg (h : DifferentiableOn ð•œ f s) : DifferentiableOn ð•œ (fun y => -f y) s :=
  fun x hx => (h x hx).neg

@[simp]
theorem differentiableOn_neg_iff : DifferentiableOn ð•œ (fun y => -f y) s â†” DifferentiableOn ð•œ f s :=
  âŸ¨fun h => by simpa only [neg_neg] using h.neg, fun h => h.negâŸ©

@[fun_prop]
theorem Differentiable.neg (h : Differentiable ð•œ f) : Differentiable ð•œ fun y => -f y := fun x =>
  (h x).neg

@[simp]
theorem differentiable_neg_iff : (Differentiable ð•œ fun y => -f y) â†” Differentiable ð•œ f :=
  âŸ¨fun h => by simpa only [neg_neg] using h.neg, fun h => h.negâŸ©

theorem fderivWithin_neg (hxs : UniqueDiffWithinAt ð•œ s x) :
    fderivWithin ð•œ (fun y => -f y) s x = -fderivWithin ð•œ f s x := by
  classical
  by_cases h : DifferentiableWithinAt ð•œ f s x
  Â· exact h.hasFDerivWithinAt.neg.fderivWithin hxs
  Â· rw [fderivWithin_zero_of_not_differentiableWithinAt h,
      fderivWithin_zero_of_not_differentiableWithinAt, neg_zero]
    simpa

/-- Version of `fderivWithin_neg` where the function is written `-f` instead of `fun y â†¦ - f y`. -/
theorem fderivWithin_neg' (hxs : UniqueDiffWithinAt ð•œ s x) :
    fderivWithin ð•œ (-f) s x = -fderivWithin ð•œ f s x :=
  fderivWithin_neg hxs

@[simp]
theorem fderiv_neg : fderiv ð•œ (fun y => -f y) x = -fderiv ð•œ f x := by
  simp only [â† fderivWithin_univ, fderivWithin_neg uniqueDiffWithinAt_univ]

/-- Version of `fderiv_neg` where the function is written `-f` instead of `fun y â†¦ - f y`. -/
theorem fderiv_neg' : fderiv ð•œ (-f) x = -fderiv ð•œ f x :=
  fderiv_neg

end Neg

section Sub

/-! ### Derivative of the difference of two functions -/


@[fun_prop]
theorem HasStrictFDerivAt.sub (hf : HasStrictFDerivAt f f' x) (hg : HasStrictFDerivAt g g' x) :
HasStrictFDerivAt (fun x => f x - g x) (f' - g') x := by
  have h_neg := hg.neg
  rw [sub_eq_add_neg]
  have h_add := HasStrictFDerivAt.add hf h_neg
  simp only [LinearMap.sub_apply, LinearMap.add_apply, map_sub, map_add, add_apply]
  exact h_add

/- ACTUAL PROOF OF HasStrictFDerivAt.sub -/

example (hf : HasStrictFDerivAt f f' x) (hg : HasStrictFDerivAt g g' x) :
    HasStrictFDerivAt (fun x => f x - g x) (f' - g') x := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg